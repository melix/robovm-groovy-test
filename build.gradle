apply plugin: 'idea'
apply plugin: 'groovy'

version = '0.1-SNAPSHOT'

repositories {
    jcenter()
}

configurations {
    robovm
}

dependencies {
    robovm  'org.codehaus.groovy:groovy:2.4.0-beta-4:grooid'
    compile 'org.codehaus.groovy:groovy:2.4.0-beta-4'
}

ext.robovmHome=System.getenv('ROBOVM_HOME')
if (!robovmHome) {
    throw new GradleException("Please set the ROBOVM_HOME environment variable")
}

List<String> sharedArgs = ['-debug'] +
    ['-cp', (files(sourceSets.main.output.classesDir)+configurations.robovm).asPath] +
    ['-d', "${buildDir}/bin"] +
    ['-o', project.name]

task roboRun(type: Exec) {
    dependsOn jar
    commandLine = ["$robovmHome/bin/robovm"]
    args += sharedArgs + ['-run', 'HelloWorld']
    doFirst {
        logger.lifecycle "Executing $commandLine"
    }
}

task roboBuild(type: Exec) {
    dependsOn jar
    commandLine = ["$robovmHome/bin/robovm"]
    args += sharedArgs + ['HelloWorld']
    doFirst {
        logger.lifecycle "Executing $commandLine"
    }
}
